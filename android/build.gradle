group 'com.dormmom.flutter_mapbox_navigation'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

def String readSdkTokenFromNetrc(String netrcFile, String repoHostname) {
    def netrcContent = "cat ${netrcFile}".execute().text.split("\n")
    boolean foundMachine = false
    String token = null;

    for (String line : netrcContent) {
        if (line.startsWith("machine") && line.substring("machine".length()).trim().equals(repoHostname)) {
            foundMachine = true
        } else if (foundMachine && token == null && line.startsWith("password")) {
            token = line.substring("password".length()).trim()
        }
    }
    return token
}

rootProject.allprojects {
    def token = System.getenv('SDK_REGISTRY_TOKEN')
    if (token == null || token.empty) {
        token = readSdkTokenFromNetrc(System.getenv('HOME') + '/.netrc', 'api.mapbox.com')
        if (token == null || token.empty) {
            throw new Exception("SDK Registry token is null. See README.md for more information.")
        }
    }
    repositories {
        google()
        jcenter()
        //maven { url 'https://mapbox.bintray.com/mapbox' }
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                // Do not change the username below.
                // This should always be `mapbox` (not your username). 
                username = "mapbox"
                // Use the secret token you stored in gradle.properties as the password
                password = token
            }
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.2.0'
    //implementation 'com.mapbox.mapboxsdk:mapbox-android-navigation-ui:0.43.0-alpha.1'
    //implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-markerview-v9:0.4.0'
    implementation "com.mapbox.navigation:ui:1.5.1"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
}
